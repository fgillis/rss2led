#!/bin/csh -f

set host_names = ( w1 w3 w4 )
# set host_names = ( red green blue fuschia )
set hosts = $#host_names

set TMP_DIR = "/tmp"
set TMP_TXT = "${TMP_DIR}/${MACHINE}_proj_proc.txt"
set OUT_TXT = "${TMP_DIR}/${MACHINE}_outp_proc.txt"
set GRP_PAT = "${LOCAL_BIN}/grep_pattern.txt"
set AWK_SCR = "${LOCAL_BIN}/proj_proc.awk"
set AWK_KIL = "${LOCAL_BIN}/proj_proc_kill.awk"
set KIL_SHL = "${TMP_DIR}/${MACHINE}_kill_proc.sh"

set INPT = "init"

set LOOP = 1
while ($LOOP >= 1)

  if(-f ${TMP_TXT}) rm ${TMP_TXT}

  echo " "
  echo " ....checking processes ....please wait."
  echo " "

  echo $hosts

  set i = 1
  while ( $i <= $hosts )

    echo "MACHINE: " $host_names[$i]			>> ${TMP_TXT}
    echo "======================================"	>> ${TMP_TXT}

    if ($MACHINE != $host_names[$i]) then
      ssh $host_names[$i] ps -ef | grep -f ${GRP_PAT}	>> ${TMP_TXT}
    else
      echo ps -ef | grep -f ${GRP_PAT}
      ps -ef | grep -f ${GRP_PAT}			>> ${TMP_TXT}
    endif

    @ i++

  end

  nawk -f ${AWK_SCR} user=${USER} ${TMP_TXT} > ${OUT_TXT}

  clear
  cat ${OUT_TXT}

  echo ""
  echo "kill options: [index number] or [machine name] or [user name]"
  echo ""
  echo "hit return to exit or 0 to rerun"
  echo ""

  set INPT = $<
  echo ""

  if ($INPT == "") then
    set LOOP = 0
  else if ($INPT == "0") then
    # do nothing
  else
    echo " ....killing processes ....please wait."
    echo ""
    nawk -f ${AWK_KIL} mach=${MACHINE} inpt=${INPT} ${OUT_TXT} > ${KIL_SHL}
    csh ${KIL_SHL}
  endif

  # cleanup temp files
  # ------------------
  rm ${TMP_TXT} ${OUT_TXT}

  if (-e ${KIL_SHL}) then
    rm ${KIL_SHL}
  endif

end

exit

